apply plugin: 'java'
apply from: 'jacoco.gradle'

def isSnapshot = { ->
    def stdout = new ByteArrayOutputStream()
    def errout = new ByteArrayOutputStream()
    def result = exec {
        commandLine 'git', 'describe', '--exact-match', '--tag', 'HEAD'
        standardOutput = stdout
        errorOutput = errout
        ignoreExitValue = true
    }
    return result.exitValue != 0
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return isSnapshot() ? stdout.toString().trim() + "-SNAPSHOT" : stdout.toString().trim()
}

sourceCompatibility = 1.7

allprojects {
    group 'org.dmfs'
    version gitVersion()
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task printVersion {
    doLast {
        println project.version
    }
}